plugins {
    id "application"
    id "idea"
    id "com.google.protobuf" version "0.8.13"
    id 'java'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def grpcVersion = '1.31.1';
def protocVersion = '3.12.0'
def ocVersion = '0.27.0';

dependencies {
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compile "io.grpc:grpc-services:${grpcVersion}"
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
    compile group: 'io.opencensus', name: 'opencensus-api', version: "${ocVersion}"
    compile group: 'io.opencensus', name: 'opencensus-exporter-trace-stackdriver', version: "${ocVersion}"
    compile group: 'io.opencensus', name: 'opencensus-exporter-stats-stackdriver', version: "${ocVersion}"
    compile group: 'io.opencensus', name: 'opencensus-contrib-grpc-metrics', version: "${ocVersion}"
    compile group: 'io.opencensus', name: 'opencensus-contrib-exemplar-util', version: "${ocVersion}"
    compile group: 'io.grpc', name: 'grpc-census', version: "${grpcVersion}"

    runtime "io.opencensus:opencensus-impl:${ocVersion}"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

startScripts.enabled = false

task helloServer(type: CreateStartScripts) {
    mainClassName = 'grpctest.HelloWorldServer'
    applicationName = 'hello-server'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
    from(helloServer)
    fileMode = 0755
}
